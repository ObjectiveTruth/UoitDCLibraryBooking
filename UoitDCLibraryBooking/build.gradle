apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play'
apply plugin: 'spoon'

def versionMajor = "1"

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        // Main Android gradle plugin allows building of the entire play toolchain
        classpath 'com.android.tools.build:gradle:2.0.0-beta6'
        classpath 'io.fabric.tools:gradle:1.+'
        // Adds tasks to auto publish apks to Google App Store. https://github.com/Triple-T/gradle-play-publisher
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.0.4'
    }
}

// Options for the publishApkRelease task. See https://github.com/Triple-T/gradle-play-publisher
play {
    jsonFile = file('google_play_android_developer_api_keys.json')
    track = 'beta' //Default track if none is specific in the commandline
}

// Options for spoon.
// Timeout added because of this issue http://stackoverflow.com/questions/34775440/spoon-timeout-getting-device-list
spoon {
    adbTimeout = 60 * 3
    debug = true
    baseOutputDir = file("$buildDir/outputs")
}

// The following 2 blocks are needed to disable animations during instrumentation testing
// See http://product.reverb.com/2015/06/06/disabling-animations-in-espresso-for-android-testing/ for steps
task grantAnimationPermission(type: Exec, dependsOn: 'installDebug') {
    def cmdToGiveSetAnimationScalePermission = "../scripts/adb_all.sh shell pm grant com.objectivetruth.uoitlibrarybooking android.permission.SET_ANIMATION_SCALE"
    commandLine cmdToGiveSetAnimationScalePermission.split(' ')
}

task uninstallAppFromAllAttachedDevices(type: Exec) {
    def cmdToUninstallDebugAppFromAllDevices =
            "../scripts/adb_all.sh shell pm uninstall com.objectivetruth.uoitlibrarybooking".split(' ')
    def cmdToUninstallTestAppFromAllDevices =
            "../scripts/adb_all.sh shell pm uninstall com.objectivetruth.uoitlibrarybooking.test".split(' ')
    commandLine cmdToUninstallDebugAppFromAllDevices
    commandLine cmdToUninstallTestAppFromAllDevices
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('spoonDebugAndroidTest')) {
        task.dependsOn grantAnimationPermission
    }
    if (task.name.startsWith('install')) {
        task.dependsOn uninstallAppFromAllAttachedDevices
    }
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

def getVersionFromContinuousIntegrationEnvironment() {
    // Added to make sure it doesn't conflict with the old way of naming which was arbitrary. Now it follows the
    // Continious Integration Environment Build Number
    def CONTINUOUS_INTEGRATION_BUILD_NUMBER_ENV_VARIABLE = "TRAVIS_BUILD_NUMBER"
    def buildNumberPrefix = 20020
    def BuildNumberFromCIWithPrefix = buildNumberPrefix +
            (System.getenv(CONTINUOUS_INTEGRATION_BUILD_NUMBER_ENV_VARIABLE) as Integer ?: 0)
    return BuildNumberFromCIWithPrefix as Integer
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"


    defaultConfig {
        applicationId "com.objectivetruth.uoitlibrarybooking"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode getVersionFromContinuousIntegrationEnvironment()
        versionName versionMajor + "." + getVersionFromContinuousIntegrationEnvironment()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release {
            storeFile file("release_key.keystore")
            storePassword System.getenv("CI_RELEASE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("CI_RELEASE_KEYSTORE_ALIAS")
            keyPassword System.getenv("CI_RELEASE_KEYSTORE_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"


    // Testing-only dependencies
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:23.2.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.3.2'
    // Excluding these modules fixed Inflation error. Reference http://stackoverflow.com/a/30715011/1293521
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }


    // App dependencies
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.github.eluleci:flatui:3.0.0'
    compile 'it.sephiroth.android.library.horizontallistview:hlistview:1.2.2'
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.easing:library:1.0.1@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true;
    }
}
